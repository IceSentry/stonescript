var text_components = []

func add_text(panel, y)
  var text = ui.AddText()
  text.anchor = top_left
  text.dock = top_left
  text.h = 1
  text.x = 1
  text.y = y
  panel.Add(text)
  return text

func init_panel()
  // init
  var panel = ui.AddPanel()
  panel.anchor = bottom_left
  panel.dock = bottom_left
  panel.x = 1
  panel.y = -1
  panel.h = 1 + 3 + 1

  // title
  var title_str = "Times"
  var title = ui.AddText(title_str)
  title.anchor = top_left
  title.dock = top_left
  title.w = string.Size(title_str)
  title.h = 1
  title.x = 1
  title.y = 0
  panel.Add(title)

  return panel

var panel
?loc.begin | loc.loop
  panel = init_panel()
  // text components
  text_components = []
  for i = 0..2
    text_components.Add(add_text(panel, i + 1))

// update

func update_text(component, new_text)
  component.text = new_text
  var size = string.Size(new_text)
  component.w = size
  return size

var current_time = 0
?loc.loop
  current_time = 0
current_time += 1

var max_size
max_size = 0
for i = 0..text_components.Count() - 1
  var str = ""
  ?i = 0
    str = "â†‘ " + time.FormatCasual(loc.bestTime)
  :?i = 1
    str = "~ " + time.FormatCasual(loc.averageTime)
  :?i = 2
    str = "= " + time.FormatCasual(current_time)
  var size = update_text(text_components[i], str)
  max_size = math.Max(max_size, size)

panel.w = 1 + max_size + 1
