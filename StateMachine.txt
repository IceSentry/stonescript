var utils = new Utils
var collection = new Collection

var combat_distance = 25
var melee_distance = 8
var _is_running = false

var states = ["out_of_combat", "combat_melee", "combat_ranged", "collecting"]
var out_of_combat = states.IndexOf("out_of_combat")
var combat_melee = states.IndexOf("combat_melee")
var combat_ranged = states.IndexOf("combat_ranged")
var collecting = states.IndexOf("collecting")

var _state = out_of_combat

func set_state(state)
  _state = states[state]

func transition(state)
  // out_of_combat
  ?state = out_of_combat
    ?foe.distance < combat_distance
      return combat_ranged
    :?collection.can_collect()
      return collecting

  // collecting
  :?state = collecting
    ?foe.distance < combat_distance
      return combat_ranged

  // ranged
  :?state = combat_ranged
    ?foe.distance > combat_distance
      return out_of_combat
    :?foe.distance < melee_distance | foe = immune_to_ranged | foe.armor > 0
      return combat_melee
  
  // melee
  :?state = combat_melee
    ?foe.distance > combat_distance
      return out_of_combat

  // state unchanged
  return state

func is_combat()
  return _state = combat_ranged | _state = combat_melee

func state()
  return states[_state]

func update()
  _state = transition(_state)

func update_debug()
  update()

  var state_str
  state_str = state()
  >o-6,7,state: @state_str@

