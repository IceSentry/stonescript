//Cooperation by Mind Stone Thief(HenriZhao) and xx.
//Special thanks to Okamiroy for his help!
//SSRPG Version : 2.28.6
//ActivationCenter Version : 2.2.2

//ActivationCenter: As a combat script, this shall be the most useful tool that
//automatically displays and controls all the in-game activation skills.
//Also, players can use Custom Inputs to adjust several parameters.

//function priority order, choose the functions you need.
//1. ActivationCenter
//2. 	AutoBardiche
//2. 	AutoBlade
//2. 	AutoDashCombo
//3. 		enableInput

//NOTE:If you have other combat script, make sure this script is BELOW all of them,
//type in the given order above, then there will be no contradictions.

//e.g.Type in the mindstone:
//var AC = new ActivationCenter
//AC.ActivationCenter(0,1)
//AC.AutoBardiche(false,400)
//AC.AutoBlade(10)
//AC.AutoDashCombo(false, false, true)
//AC.enableInput()

//Useful inputs: 
//press X to stop the Bardiche's auto attack, press C to conceal the whole table.
//press arrow keys to adjust the bardiche attack's threshold.

var ActivationCenterTable = ascii
╔═Cooldowns═╗
║BDC-ATK    ║
║DS-DASH    ║
║QS-DASH    ║
║MS-DODG    ║
╚═══════════╝
asciiend

var oposX = 0
var oposY = 0

var checktable = -1
var checkBardiche = -1

var hupt = 0
var hdwt = 0

//Private function:Count the left Cooldown Time, if item.GetCooldown("item")<= 0, return RDY, else return the true Cooldown Time in seconds.
func CooldownDisplay(item)
	?item.GetCooldown(item) = 0
		return "RDY"

  var CurCD = item.GetCooldown(item) / 30
  return CurCD + "s"

//Private function : Color control for display.
func Color(itemt)
	?string.Equals(itemt, "RDY")
		return rainFF
	:
		return red

//ActivationCenterPanel:Only show a table with activation time left
//posX,posY is the coordinate of AC(relative to the upper-left)
func ActivationCenter(posX,posY)
	checktable = totaltime
	oposX = posX
	oposY = posY

	var col1 = posX + 10
	var row
	var cooldown
	var color
	>`@posX@,@posY@,#white,@ActivationCenterTable@

	//row 1 (bardiche)
	row = posY + 1
	cooldown = CooldownDisplay(bardiche)
	color = Color(cooldown)
	>`@col1@,@row@,#@color@,@cooldown@

	//row 2 (dash)
	row = row + 1
	cooldown = CooldownDisplay(dash)
	color = Color(cooldown)
	>`@col1@,@row@,#@color@,@cooldown@

	//row 3 (quarterstaff)
	row = row + 1
	cooldown = CooldownDisplay(quarterstaff)
	color = Color(cooldown)
	>`@col1@,@row@,#@color@,@cooldown@

	//row 4 (mind)
	row = row + 1
	cooldown = CooldownDisplay(mind)
	color = Color(cooldown)
	>`@col1@,@row@,#@color@,@cooldown@


//Bardiche auto control panel
//BDCAtkBossOnly = true/false
//BDCthreshold is the total number of current foe's (hp+armor) that activates Bardiche's attack.It is adjustable.(0-5000)
func AutoBardiche(BDCAtkBossOnly, BDCthreshold)
	checkBardiche = totaltime

	?(foe = boss | !BDCAtkBossOnly) & foe ! wasp & foe ! ghost_tomb & foe ! explode &
	^(foe.hp + foe.armor) >= BDCthreshold &
	^foe.distance <= 10 & item.CanActivate() &
	^(item.GetCooldown("bardiche") <= 0 | item.GetCooldown("bardiche") >= 882)
		equip bardiche 
		activate R

//Private function: Smite track. You may even use in your other scripts to track the Smite number.
func smiteTrack()
	var status = buffs.string
	var index
	var digit

	index = string.IndexOf(status, "smite:")

	?index ! -1
		index = index + 6
		digit = string.IndexOf(status, ":", index) - index
		return int.Parse(string.Sub(status, index, digit))
	:
		return 0

//Auto blade of fallen god control panel.
//BFGthresholdNO is the minimum count of foes that activates the blade.
func AutoBlade(BFGthresholdNO)
	?foe.count > BFGthresholdNO & item.GetCooldown("blade") <= 0
		equip blade 
		activate R
	
	?checktable = totaltime
		var BFGFTNOposX = oposX + 14
		var BFGsmiteNOposX = oposX + 22
		var ABFGposY = oposY + 2

		var smiteCount = smiteTrack()

		>`@BFGFTNOposX@,@ABFGposY@,#white,?@foe.count@
		BFGFTNOposX = BFGFTNOposX + 3
		>`@BFGFTNOposX@,@ABFGposY@,#white,>@BFGthresholdNO@

		?smiteCount > 0
			>`@BFGsmiteNOposX@,@ABFGposY@,#FF00FF,•@smiteCount@
