var state_machine = import StateMachine
var collection = import Collection
var potion = import Potion

var utils = new Utils
var state_machine = new StateMachine

// ======
// consts
// ======

var fissure_cooldown = 18
var fissure_charge = 1
var heal_threshold = 70 //%

// =========
// variables
// =========

var current_potion
current_potion = potion.get_current_potion()

var fissure_timer = 0
?loc.loop
  fissure_timer = 0

var is_ki_farming = false
?current_potion = "experience"
  is_ki_farming = true
  >ki farming enabled 

// ==========
// properties
// ==========

func hp_percentage()
  return utils.calc_percentage(hp, maxhp)

func armor_percentage()
  return utils.calc_percentage(armor, maxarmor)

// =======
// methods
// =======

func equip_fissure()
  >fissure @fissure_timer@

  ?fissure_timer > 0 & time % 30 = 0
    fissure_timer-- // tick every second

  ?!item.CanActivate()
    return

  ?fissure_timer = 0
    fissure_timer = fissure_cooldown + fissure_charge

  ?fissure_timer >= fissure_cooldown
    state_machine.start_casting()
    equipL fissure // need to equip it for a while to charge
    // TODO equip +heal offhand for more damage
  :
    state_machine.stop_casting()

func equip_melee()
  // left
  ?foe = immune_to_physical & foe ! immune_to_ranged
    equipL wand
  :?foe.armor > 0
    equipL aether hammer
  :?foe.count > 2
    equipL big vigor sword
  :
    equipL vigor d_sword

  // right
  ?is_ki_farming & hp_percentage() = 100 & foe ! boss
    equipR ki
  :?(foe = immune_to_stun | foe.count > 2) & hp_percentage() <= heal_threshold
    equipR vigor shield
  :?foe ! immune_to_stun  & foe.debuffs.string ! stun
    equipR compound shield
  :?foe.count > 2 | foe.armor > 0
    equipR vigor d_sword
  :
    equipR big vigor sword

func equip_ranged()
  ?hp_percentage() >= heal_threshold
    ?(foe = magic_resist | foe ! magic_vulnerability) & foe ! immune_to_physical
      equip repeating crossbow
      return
      
    ?foe = vigor
      equipL p wand
    :
      equipL vigor wand

    ?is_ki_farming & foe ! boss
      equipR ki
    :        
      equipR moondial // attack speed
  :
    equipL vigor wand
    equipR vigor shield

func dodge()
  ?item.GetCooldown("mind") = 0
    equipL mind

func dash()
  ?item.CanActivate() & item.GetCooldown("quarterstaff") = 0
    equip quarterstaff
    activate R

func combat_dash()
  ?item.GetCooldown("bash") = 0
    equipR bashing shield
  :?item.GetCooldown("dash") = 0
    equipL triskelion
    equipR dashing shield

func equip_passive_regen()
  equipR compound shield // generates armor
  equipL ouroboros // passive heal

// =================
// Update
// =================

state_machine.update()

// =================
// Use state machine
// =================

?state_machine.state() = out_of_combat
  equip_passive_regen()
  ?hp_percentage() >= 75 & !ai.idle
    equipL triskelion // gotta go fast
    dash()

:?state_machine.state() = boss_idle
  equip_passive_regen()

:?state_machine.state() = collecting
  collection.collect()

:?state_machine.state() = combat_ranged
  equip_ranged()

:?state_machine.state() = combat_melee
  ?foe = wasp
    equip_ranged()
  :
    equip_melee()
    ?foe.distance > 11
      combat_dash()

:?state_machine.state() = casting
  >o1,1,casting

// ====
// Boss
// ====

?foe = boss & state_machine.is_combat() | state_machine.state() = casting
  ?foe.debuffs.count = 0 & foe ! immune_to_debuff_damage &
  ^foe.name ! bronze &
  ^(foe.name ! dysangelos & foe ! phase2) // there's a weird part where he's somehow immune to debuff
    equipR dp wand

  ?foe.distance <= 10 & item.CanActivate() & 
  ^(item.GetCooldown("bardiche") = 0 | item.GetCooldown("bardiche") >= 882)
    equip bardiche
    activate R

  ?foe.name = bolesh
    // ?foe.distance > 11
    //   combat_dash()
    equip repeating crossbow
    ?(foe.state = 133 & foe.time = 1) | (foe.state = 143 & foe.time = 0 )
      dodge()
  :?foe.name = angry shroom
    // TODO handle salvo
    ?foe.state = 32 & foe.time = 50
      dodge()
  :?foe.name = morel
    ?foe.state = 33 & foe.time = 0
      dodge()
    :?foe.distance > 11
      combat_dash()
  :?foe.name = xyloalgia
    // equip_fissure()
    ?foe.distance > 11
      combat_dash()
    ?foe.state = 33 & foe.time = 15 & state_machine.state() ! casting
      dodge()
  :?foe.name = bronze
    ?foe.state = 2
      equip repeating crossbow
    :?foe.state = 33 & foe.distance > 11
      combat_dash()
    :?foe.state = 32 & foe.time = 30
      dodge()
  :?foe.name = nagaraja
    ?foe.distance > 11
      combat_dash()
    ?foe.state = 33 & foe.time = 20
      dodge()
  :?foe.name = dysangelos
    ?foe.distance > 11
      combat_dash()
  :?foe.name = hrimnir
    ?foe.state = 33 | foe.state = 32
      equip repeating crossbow

// ======
// Potion
// ======

?current_potion = "healing" & hp_percentage() <= 15
  activate potion
:?current_potion = "experience" & state_machine.is_combat()
  activate potion 
:?current_potion = "cleansing" & debuffs.count > 6 | hp_percentage() < 35
  activate potion
