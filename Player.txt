var state_machine = import StateMachine
var collection = import Collection
var ki_farming = import KiFarming

var utils = new Utils
var state_machine = new StateMachine

// ======
// consts
// ======

var fissure_cooldown = 18
var fissure_charge = 1

// =========
// variables
// =========

var is_casting = false
var fissure_timer = 0

// ==========
// properties
// ==========

func hp_percentage()
  return utils.calc_percentage(hp, maxhp)

func armor_percentage()
  return utils.calc_percentage(armor, maxarmor)

// =======
// methods
// =======

func equip_fissure()
  ?!item.CanActivate()
    return

  ?fissure_timer > 0 & time % 30 = 0
    fissure_timer-- // tick every second 
  :?fissure_timer = 0
    fissure_timer = fissure_cooldown + fissure_charge

  ?fissure_timer >= fissure_cooldown
    >fissure @fissure_timer@
    is_casting = true
    equipL fissure // need to equip it for a while to charge
    // TODO equip +heal offhand for more damage
  :
    is_casting = false

func equip_melee()
  // left
  ?foe = immune_to_physical & foe ! immune_to_ranged
    equipL wand
  :?foe.armor > 0
    equipL hammer
  :
    equipL vigor sword

  // right
  ?foe = immune_to_stun | hp_percentage() <= 90
    equipR vigor shield
  :?ki_farming.is_farming & hp_percentage() = 100 & foe ! boss
    equipR ki
  :
    equipR compound shield

func equip_ranged()
  ?foe = magic_resist & foe ! immune_to_physical
    equip repeating crossbow
    return

  // left
  ?foe = vigor
    equipL p wand
  :
    equipL vigor wand

  // right
  ?hp_percentage() >= 90
    ?ki_farming.is_farming & foe ! boss
      equipR ki
    :        
      equipR moondial // attack speed
  :
    equipR vigor shield

func potion()
  ?ki_farming.is_farming & state_machine.is_combat()
    activate potion // wood
  :?debuffs.count > 5 | hp_percentage() < 40
    activate potion // cleansing

func dodge()
  ?item.GetCooldown("mind") = 0
    equipL mind

func dash()
  // quarterstaff
  ?item.CanActivate() & item.GetCooldown("quarterstaff") = 0
    equip quarterstaff
    activate R

func combat_dash()
  ?item.GetCooldown("bash") = 0
    equipR bashing shield
  :?item.GetCooldown("dash") = 0
    equipR dashing shield

// =================
// Update
// =================

ki_farming.update()
state_machine.update_debug()

// =================
// Use state machine
// =================

?state_machine.state() = out_of_combat
  equipR compound shield
  equipL ouroboros // passive heal

  ?hp_percentage() >= 90
    equipL triskelion // gotta go fast
    dash()

:?state_machine.state() = collecting
  collection.collect()

:?state_machine.state() = combat_ranged
  equip_ranged()

:?state_machine.state() = combat_melee
  equip_melee()
  // TODO bardiche check
  ?foe.distance > 11
    combat_dash()

// =================
// Boss stuff
// =================

?foe = boss & state_machine.is_combat()
  var timemod
  timemod = foe.time / 10
  >f0,8,state: @foe.state@ t: @timemod@

  ?foe.debuffs.count = 0 & foe ! immune_to_debuff_damage &
  ^(foe.name = dysangelos & foe = phase2) // there's a weird part where he's somehow immune to debuff
    equipR dp wand

  equip_fissure()

  ?foe.name = bolesh
    ?foe.state = 133 & foe.time = 1
      dodge()
  :?foe.name = angry shroom
    // TODO handle salvo
    ?foe.state = 32 & foe.time = 50
      dodge()
  :?foe.name = morel
    ?foe.state = 33 & foe.time = 0
      dodge()
  :?foe.name = xyloalgia
    ?foe.state = 33 & foe.time = 15
      dodge()


// prioritize the potion
potion()
