var cooldowns = ["dash", "quarterstaff", "mind", "blade"]
var tags = ["DSH", "STF", "MND", "BLD"]
var cd_text_components = []

func add_text(panel, y)
  var text = ui.AddText()
  text.anchor = top_left
  text.dock = top_left
  text.h = 1
  text.x = 1
  text.y = y
  return text

func init_panel()
  var cooldown_panel = ui.AddPanel()
  cooldown_panel.anchor = bottom_left
  cooldown_panel.dock = bottom_left
  cooldown_panel.x = 1
  cooldown_panel.y = -6
  cooldown_panel.h = 1 + cooldowns.Count() + 1

  // title
  var title_str = "CD"
  var title = ui.AddText(title_str)
  title.anchor = top_left
  title.dock = top_left
  title.w = string.Size(title_str)
  title.h = 1
  title.x = 1
  title.y = 0
  cooldown_panel.Add(title)

  return cooldown_panel

var cooldown_panel
?loc.begin | loc.loop
  // init panel
  cooldown_panel = init_panel()

  // text components
  cd_text_components = []
  for i = 0..cooldowns.Count() - 1
    var comp = add_text(cooldown_panel, i + 1)
    cd_text_components.Add(comp)
    cooldown_panel.Add(comp)


// update

func update_text(component, new_text)
  component.text = new_text
  var size = string.Size(new_text)
  component.w = size
  return size

var max_size
max_size = 0
for i = 0..cooldowns.Count() - 1
  var text = tags[i] + " " + time.FormatCasual(item.GetCooldown(cooldowns[i]))
  var size = update_text(cd_text_components[i], text)
  max_size = math.Max(max_size, size)

cooldown_panel.w = 1 + max_size + 1
