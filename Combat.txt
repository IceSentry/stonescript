var utils = new Utils

var combat_distance = 22
var melee_distance = 8

var hp_percentage
hp_percentage = utils.calc_percentage(hp, maxhp)

var armor_percentage
armor_percentage = utils.calc_percentage(armor, maxarmor)

// utils 

func is_immune_to(type)
  return foe = "immune_to_" + type

// actions

func equip_melee()
  // left
  ?is_immune_to("physical") & !is_immune_to("ranged")
    equipL wand
  :?foe.armor > 0
    equipL hammer
  :
    equipL big sword

  // right
  ?is_immune_to("stun")
    equipR vigor shield
  :
    equipR compound shield

func equip_ranged()
  ?foe = magic_resist
    equip heavy crossbow
  :
    // left
    equipL wand
    // right
    ?hp_percentage = 100
      equipR moondial
    :
      equipR vigor shield

func equip_stone()
  ?hp_percentage <= 95
    equipL ouroboros // passive heal
  :
    equipL triskelion // gotta go fast

func potion()
  ?debuffs.count > 5 | hp_percentage < 40
    activate potion


var dodge_frame = 0
?loc.loop
  dodge_frame = 0
func dodge()
  var delta = time - dodge_frame
  ?delta >= 3600 | dodge_frame = 0
    equipL mind
    dodge_frame = time

// states

var states = ["out_of_combat", "combat_melee", "combat_ranged"]
var out_of_combat = states.IndexOf("out_of_combat")
var combat_melee = states.IndexOf("combat_melee")
var combat_ranged = states.IndexOf("combat_ranged")

var _state = out_of_combat

func set_state(state)
  _state = state


potion()

?_state = out_of_combat
  equip_stone()
  ?foe.distance < combat_distance
    set_state(combat_ranged)

:?_state = combat_ranged
  ?is_immune_to("ranged") | foe.armor > 0
    set_state(combat_melee)

  equip_ranged()

  ?foe.distance > combat_distance
    set_state(out_of_combat)

:?_state = combat_melee
  equip_melee()

  ?foe.distance > combat_distance
    set_state(out_of_combat)

?foe = boss
  >f8,1,state: @foe.state@

  ?foe.debuffs.count = 0 & !is_immune_to("debuff_damage")
    equip dp

  ?foe.name = bolesh
    ?foe.state = 133 & foe.time = 1
      dodge()

// var pretty
// pretty = states[_state]
// >state: @pretty@
